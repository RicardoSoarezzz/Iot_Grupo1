#include <WiFi.h>
#include <PubSubClient.h>
#include <HardwareSerial.h>
#include <ArduinoJson.h>

const char* ssid = "DuckNet";
const char* password = "DuckieUPT";
const char* mqtt_server = "192.168.0.101";
#define mqtt_port 1883
#define TOPIC "/ic/Grupo1"
#define TOPIC_INTERFACE "/ic/Grupo1Interface"

#define rxPin 16
#define txPin 17

HardwareSerial sw(2);

WiFiClient wifiClient;
PubSubClient mqttClient(wifiClient);

const char* esp_id = "ESP32_1";

void setup_wifi() {
    delay(10);
    Serial.println();
    Serial.print("Connecting to ");
    Serial.println(ssid);
    WiFi.begin(ssid, password);
    while (WiFi.status() != WL_CONNECTED) {
        delay(500);
        Serial.print(".");
    }
    Serial.println("");
    Serial.println("WiFi connected");
    Serial.print("IP address: ");
    Serial.println(WiFi.localIP());
}

void reconnect() {
    while (!mqttClient.connected()) {
        Serial.print("Attempting MQTT connection...");
        char clientId[50];
        snprintf(clientId, sizeof(clientId), "Grupo1_RM_%s", esp_id);
        if (mqttClient.connect(clientId)) {
            Serial.println("MQTT Connected Successfully");
            mqttClient.subscribe(TOPIC);
        } else {
            Serial.print("Connection failed, rc=");
            Serial.print(mqttClient.state());
            Serial.println(" Trying again in 5 seconds...");
            delay(5000);
        }
    }
}

void setup() {
    pinMode(rxPin, INPUT);
    pinMode(txPin, OUTPUT);

    Serial.begin(115200);
    sw.begin(19200, SERIAL_8N1, rxPin, txPin);

    setup_wifi();
    mqttClient.setServer(mqtt_server, mqtt_port);
    mqttClient.setCallback(callback);
    reconnect();
}

void loop() {
    mqttClient.loop();
    if (sw.available() > 0) {
        char bfr[501];
        memset(bfr, 0, 501);
        sw.readBytesUntil('\n', bfr, 500);
        String b = String(bfr);
        publishSerialData(b);
    }
    delay(200);
}

String tiraAspas(String s) {
    if (s.charAt(0) == '"')
        s = s.substring(1);
    if (s.charAt(s.length() - 1) == '"')
        s = s.substring(0, s.length() - 1);
    return s;
}

void publishSerialData(String json) {
    if (!mqttClient.connected()) {
        reconnect();
    }

    Serial.print("Raw JSON: ");
    Serial.println(json);

    StaticJsonDocument<200> doc;

    DeserializationError error = deserializeJson(doc, json);

    if (error) {
        return;
    }

    const char* topico = doc["topico"];
    const char* tagName = doc["tagName"];
    int valor = doc["valor"];

    doc["esp_id"] = esp_id;

    String dados = String(tagName) + ":" + valor;

    mqttClient.publish(topico, dados.c_str());

    Serial.print("Publish : ");
    Serial.print(topico);
    Serial.print(" : ");
    Serial.print(dados);
    Serial.println();
}

void callback(char* topic, byte* payload, unsigned int length) {
    Serial.print("Message received on topic: ");
    Serial.print(topic);
    String st = "";
    for (unsigned int i = 0; i < length; i++) {
        st += (char)payload[i];
    }
    st.trim();
    Serial.println(st);

    StaticJsonDocument<200> doc;

    DeserializationError error = deserializeJson(doc, st);

    if (error) {
        Serial.print("deserializeJson() failed: ");
        Serial.println(error.f_str());
        return;
    }

    const char* topico = doc["topico"];
    const char* tagName = doc["tagName"];
    int valor = doc["valor"];

    String message = "{\"topico\":\"" + String(topico) + "\",\"tagName\":\"" + String(tagName) + "\",\"valor\":" + String(valor) + "}";
    sw.println(message);
}
